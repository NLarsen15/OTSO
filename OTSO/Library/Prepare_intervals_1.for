c  This code goes over the yearly OMNI files (with short gaps filled by linear interpolation
c    and SYM-H index added) and creates a list of intervals, for which the TS05 model parameters
c     W1-W6 can be calculated
c

C
c   Author:  N. A. Tsyganenko, University of St.Petersburg
c   Date:    09/05/2008, 10/13/2008
c
      SUBROUTINE PREPAREINTERVALS1(OMNIYEAR)
      implicit real*8 (a-h,o-z)

      INTEGER*4 :: OMNIYEAR
C
      CHARACTER*80 SWNAME,NAMEOUT
      DIMENSION IDAY(105408),IHOUR(105408),MIN(105408),BXGSM(105408),
     * BYGSM(105408),BZGSM(105408),VGSE(105408),VXGSE(105408),
     * VYGSE(105408),VZGSE(105408),TEMP(105408),DEN(105408),
     * SYMH(105408),IMFFLAG(105408),ISWFLAG(105408)

      SYMH_LOWLIM=-10.D0  !  LOW LIMIT ON SYMH DURING THE 2-HOUR "QUIET-TIME PERIOD"
C                              PRECEDING EACH SELECTED INTERVAL
      DSYMH_LIM=    5.D0  !  UPPER LIMIT ON THE SYMH VARIATION RANGE DURING THE ABOVE QUIET PERIOD
C
c      DO 8 IYEAR=1995,2013

      IYEAR = int(OMNIYEAR)

      WRITE(SWNAME, '(I0, "_IMF_&_SW_gaps_le_3hrs_filled.txt")') IYEAR
      WRITE(NAMEOUT, '(I0, "_Interval_list.txt")') IYEAR

c
C     READ THE INTERPLANETARY/SYMH DATA SET:
C
      OPEN (UNIT=1,FILE=SWNAME,STATUS='OLD')     !  filename for the solar wind/IMF/Sym-H data
 505  FORMAT(2I4,2I3,F8.2,2F8.2,4F8.1,F7.2,F9.0,F6.0,3X,I2,3X,I2)
C
      NREC=0
C
 1    READ(1,505,END=2) IYEA,IDA,IHOU,MI,BX,BY,BZ,V,VX,VY,VZ,DE,T,SYMHI,
     * IMFLAG,ISFLAG
      NREC=NREC+1
      MINYEAR=(IDA-1)*1440+IHOU*60+MI
      IND=MINYEAR/5+1
      IDAY (IND)=IDA
      IHOUR(IND)=IHOU
      MIN  (IND)=MI
      BXGSM(IND)=BX
      BYGSM(IND)=BY
      BZGSM(IND)=BZ
      VGSE (IND)=V
      VXGSE(IND)=VX
      VYGSE(IND)=VY
      VZGSE(IND)=VZ
C      VTH  (IND)=0.15745*SQRT(T)  !  SQRT(3kT/M_p) in km/s
      TEMP (IND)=T
      DEN  (IND)=DE
      SYMH (IND)=SYMHI
      IMFFLAG(IND)=IMFLAG
      ISWFLAG(IND)=ISFLAG
      GOTO 1

 2    CLOSE(1)

      !PRINT *, '  READING OF OMNI DATA FINISHED'
C
C  NOW GO OVER THE CREATED ARRAYS, RECORD-BY-RECORD, AND FIND A SET OF INTERVALS,
C  WHOSE FIRST RECORD IS PRECEDED BY NO LESS THAN 2-HOUR CONTINUOUS QUIET PERIOD,
C  AND ITS LAST RECORD IS IMMEDIATELY FOLLOWED BY A NO-DATA PERIOD (ALL NO-DATA
C  PERIODS HERE ARE LONGER THAN 2 HOURS, SINCE THE SHORTER ONES WERE PREVIOUSLY
C  FILLED IN USING LINEAR INTERPOLATION).
C
C  HERE WE DEFINE THE QUIET PERIOD AS THE ONE, SATISFYING THE FOLLOWING REQUIREMENTS:
C   (i)   NO DATA GAPS (I.E., BOTH IMFFLAG AND ISWFLAG NOT EQUAL -1)
C   (ii)  IMF BZGSM => 0 FOR EACH RECORD IN A ROW
C   (iii) SYM-H => -10 FOR EACH RECORD IN A ROW
C
      OPEN (UNIT=1,FILE=NAMEOUT)
      NUMBER=0  !  TOTAL NUMBER OF RECORDS IN THE SELECTED INTERVALS
      LENGTH_QUIET=0
      IND=0  !  IND IS THE # OF THE CURRENT RECORD; HERE WE INITIALIZE IT WITH 0
C
      SYMHMAX=-1000.
      SYMHMIN=+1000.
C
C  NOW SEARCH FOR THE RECORD THAT CAN BE A CANDIDATE FOR THE FIRST RECORD OF A QUIET-TIME INTERVAL
C
  3   IND=IND+1
       IF (IND.EQ.NREC) GOTO 6
       IF (SYMH(IND).GT.SYMHMAX) SYMHMAX=SYMH(IND)
       IF (SYMH(IND).LT.SYMHMIN) SYMHMIN=SYMH(IND)
C
       IF ((IMFFLAG(IND).EQ.-1).OR.(ISWFLAG(IND).EQ.-1).OR.
     *     (BZGSM(IND).LT.0.D0).OR.(SYMH(IND).LT.SYMH_LOWLIM)) THEN
         LENGTH_QUIET=0
         SYMHMAX=-1000.
         SYMHMIN=+1000.
         GOTO 3  
C         !THE RECORD DIDN'T MEET THE FIRST 3 REQUIREMENTS (DATA AVAILABLE, IMF_BZ>=0, SYMH>=SY,H_LOWLIM),
C                   SO SKIP IT AND GO TO THE NEXT ONE.
       ENDIF

       IF (LENGTH_QUIET.EQ.0) IND_FIRST_GOOD=IND  !  MARKS THE FIRST RECORD THAT SATISFIED THE FIRST 3 REQUIREMENTS

       IF (SYMHMAX-SYMHMIN.GT.DSYMH_LIM) THEN
        IND=IND_FIRST_GOOD+1
        LENGTH_QUIET=0
        SYMHMAX=-1000.
        SYMHMIN=+1000.
        GOTO 3
       ENDIF
C
       LENGTH_QUIET=LENGTH_QUIET+1    !  FIRST SATISFACTORY RECORD HAS BEEN REACHED
C
       IF (LENGTH_QUIET.EQ.24) THEN   !  LENGTH OF THE CURRENT QUIET INTERVAL REACHED 2 HRS, SO
C                                        LET US GO OVER THE FOLLOWING SEQUENCE OF RECORDS, UNTIL A GAP IS ENCOUNTERED,
C                                        AND WRITE IN THE OUTPUT FILE THE NUMBERS OF ITS FIRST AND LAST RECORDS.
C
       INDBEG=IND   ! INDBEG CORRESPONDS TO THE LAST RECORD OF THE QUIET-TIME PERIOD
C
        DO 5 IND=INDBEG,NREC
          IF ((IMFFLAG(IND).EQ.-1).OR.(ISWFLAG(IND).EQ.-1)) THEN  !  FIRST RECORD OF THE NEXT GAP ENCOUNTERED
            INDEND=IND-1
            INDBEGIN=INDBEG-LENGTH_QUIET+1   !  HERE INDBEGIN CORRESPONDS TO THE BEGINNING OF THE FIRST RECORD
            WRITE (1,100) INDBEGIN,INDEND  !       OF THE 2-HR QUIET INTERVAL
            NUMBER=NUMBER+(INDEND-INDBEGIN+1)
 100        FORMAT((2I10))
            LENGTH_QUIET=0
            SYMHMAX=-1000.
            SYMHMIN=+1000.
            GOTO 3
          ENDIF  !  END OF THE CASE "IMFFLAG(IND).EQ.-1).OR.(ISWFLAG(IND).EQ.-1"
  5     CONTINUE
C
C  THE DO 5 LOOP WAS FULLY EXECUTED, WHICH MEANS THAT WE ARRIVED AT IND=NREC; IN THIS CASE, THE LAST RECORD
C  OF THE YEARLY FILE IS AT THE SAME TIME THE LAST RECORD OF THE LAST GOOD SEQUENCE;
C  LET US INCLUDE IT IN THE OUTPUT FILE AND THEN EXIT:
C
            INDEND=NREC
            INDBEGIN=INDBEG-LENGTH_QUIET+1
            WRITE (1,100) INDBEGIN,INDEND
            NUMBER=NUMBER+(INDEND-INDBEGIN+1)
            GOTO 6
       ENDIF   !    END OF THE CASE "IF (LENGTH_QUIET.EQ.24)"
      GOTO 3

 6    CLOSE(1)

C      PRINT *,
C     *' TOTAL # OF RECORDS IN ALL SELECTED INTERVALS FOR THIS YEAR:',
C     * NUMBER
C      PRINT*,'  PERCENTAGE COVERED:', FLOAT(NUMBER)/FLOAT(NREC)*100.,'%'

C
  8   CONTINUE

C     PAUSE
C
      END SUBROUTINE PREPAREINTERVALS1

C**************************************************************************